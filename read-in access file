DM LOG "CLEAR" LOG;
%LET PATH= \\DATAPREP;
LIBNAME SASDATA "&PATH.\SASDATA";

/***********************************************/
/*** MACRO FOR CONTROL TOTALS 
/***********************************************/

%MACRO CONTOT(LIBNAME =,DATASET=,VAR=);
       PROC SUMMARY DATA= &LIBNAME..&DATASET. NWAY MISSING;
            VAR &VAR.;
            OUTPUT OUT = &DATASET._SUMM (KEEP = _FREQ_ &VAR.) SUM=;
       RUN;
       %LET CNT = %EVAL(((%SYSFUNC(LENGTH(&VAR.))) - (%SYSFUNC(LENGTH(%SYSFUNC(COMPRESS(&VAR.))))))+1);
       DATA _NULL_;
            SET &DATASET._SUMM;
            FORMAT _FREQ_ COMMA30.;
            FORMAT &VAR. DOLLAR30.2;
            PUT @5 "---------------------------------------------------------------------------------------"/;
            PUT @5 "RECORD COUNT & CONTROL TOTALS FOR &DATASET. "/;
            PUT @5 "RECORD COUNT = " _FREQ_/;
                %DO I = 1 %TO &CNT.;
                %LET VAR1 = %SCAN(&VAR.,&I.);
                     PUT @5 "&VAR1. = " &VAR1./;
                %END;
            PUT @5 "---------------------------------------------------------------------------------------"/;
      RUN;
%MEND;

******************************************************************************;
***              MACRO TO EXPORT DATASETS INTO EXCEL
******************************************************************************;
%MACRO EXPORT_EXCEL(DATASET,OUTFILE,SHEET);
PROC EXPORT DATA= SASDATA.&DATASET.
            OUTFILE= "&PATH.\OUTPUT\&OUTFILE..XLSX" 
            DBMS=XLSX REPLACE;
            SHEET="&SHEET."; 
       RUN;
%MEND;

******************************************************************************;
***              MACRO TO EXPORT DATASETS IN PIPE DELIMITED FORMAT
******************************************************************************;
%MACRO EXPORT_DELIM(DATASET);
       PROC EXPORT DATA=SASDATA.&DATASET. 
			OUTFILE="&PATH.\OUTPUT\&DATASET..TXT"
			DBMS=DLM
			REPLACE;
			DELIMITER='|';
       RUN;
%MEND;


%MACRO JEREADIN(IN,OUT);
PROC IMPORT OUT=SASDATA.JE_&OUT.
	DATATABLE= "&IN." 
	DBMS=ACCESSCS REPLACE;
	DATABASE="\\DATAPREP\RAWDATA\&IN..ACCDB";
RUN;
%MEND;

%JEREADIN(201401,1);
%JEREADIN(201402,2);
%JEREADIN(201403,3);
%JEREADIN(201404,4);
%JEREADIN(201405,5);
%JEREADIN(201406,6);
%JEREADIN(201407,7);
%JEREADIN(201408,8);
%JEREADIN(201409,9);
%JEREADIN(MEMBER,10);
 


DATA SASDATA.JE;
RETAIN YOS SERVICE_MONTH PAY_MONTH LOBD_ID IPCD_ID CSPI_ID SGSG_ID CLCL_LOW_SVC_DT_NEW CLCL_PAID_DT_NEW CDML_PAID_AMT;
SET SASDATA.JE_1 SASDATA.JE_2 SASDATA.JE_3 SASDATA.JE_4 SASDATA.JE_5 SASDATA.JE_6 SASDATA.JE_7 SASDATA.JE_8 SASDATA.JE_9 SASDATA.JE_10;
CLCL_LOW_SVC_DT_NEW= INPUT(SUBSTR(COMPRESS(PUT(CLCL_LOW_SVC_DT,DATETIME20.)),1,9),DATE9.) ;
CLCL_PAID_DT_NEW=INPUT(SUBSTR(COMPRESS(PUT(CLCL_PAID_DT,DATETIME20.)),1,9),DATE9.);
FORMAT CLCL_LOW_SVC_DT_NEW CLCL_PAID_DT_NEW MMDDYY10.;
YOS=YEAR(CLCL_LOW_SVC_DT_NEW);
SERVICE_MONTH=COMPRESS(PUT(YOS,12.))||SUBSTR(PUT(CLCL_LOW_SVC_DT_NEW,MMDDYY10.),1,2);
PAY_MONTH=COMPRESS(PUT(YEAR(CLCL_PAID_DT_NEW),12.))||SUBSTR(PUT(CLCL_PAID_DT_NEW,MMDDYY10.),1,2);
RUN;


PROC SUMMARY DATA = SASDATA.JE NWAY MISSING;
CLASS CLCL_PAID_DT_NEW LOBD_ID SGSG_ID;
VAR  CDML_PAID_AMT;
OUTPUT OUT = SASDATA.PAYDATE_SUMM(DROP = _TYPE_ RENAME = (_FREQ_ = JE_COUNT)) SUM=;
RUN;


PROC SUMMARY DATA = SASDATA.JE NWAY MISSING;
CLASS YOS SERVICE_MONTH PAY_MONTH LOBD_ID SGSG_ID ;
VAR  CDML_PAID_AMT;
OUTPUT OUT = SASDATA.SERVICEMONTH_SUMM(DROP = _TYPE_ RENAME = (_FREQ_ = JE_COUNT)) SUM=;
RUN;

%EXPORT_EXCEL(PAYDATE_SUMM,SUMMARY,PAYDATE_SUMM);
%EXPORT_EXCEL(SERVICEMONTH_SUMM,SUMMARY,SERVICEMONTH_SUMM);
